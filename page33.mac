;	PAGE 33 - TAPE INTERRUPT ROUTINES

TIP:	CONI 	MT1,TEND4;	 READ IN TAPE FLAGS
	CONSO	MT1,4;		 ERF INTERRUPT?
	JRST	TE20

TEND:	CONO	MT1,0;		DISABLE THE INTERRUPTS
	EXCH	S,TEND5
	EXCH	B,TEND6
	EXCH	E,TEND7;	GIVE ME SOME ELBOW ROOM
	EXCH	PP,TPDL
	MOVE	B,TEND4;	GET THE FLAGS
	TRNE	B,70;		SKIP IF NO ERROR
	JRST	TEND9;		GO FIX THE ERROR
	SETZM	TEND8;		OK, ZERO THE RETRY COUNT
	SKIPE	TEND4;		RESTART DO DONT GIVE BACK BUFFER
	TSX	MBA
	SKIPE	DT.BUF;		SKIP IF NO MORE BUFFERS
TEND1:	TSX	CIO
TEND2:	EXCH	S,TEND5
	EXCH	B,TEND6
	EXCH	E,TEND7
	EXCH	PP,TPDL
	JRST	12,@CTYR

TE20:	SKIPN	TEND8;		SKIP TO IGNORE BKSPACE OR WRITE BLANK
	JRST	TE22;		GO TO TRY TO READY TAPE
TE21:	CONO	MT1,0;		KILL THE INTERRUPT
	JRST	12,@CTYR
TE22:	CONSZ	PI,CHDC;	GET IT LATER IF DATA CONTROL IS BUSY
	JRST	TE21
	CONSO	MT1,2;		SKIP IF TRANSPORT READY
	JRST	TE21;		RETURN WE WILL TRY AGAIN LATER
	SETZM	TEND4;		SET RESTART SIGNAL
	JRST	TEND;		GO DO IT

TEND4:	Z;			FLAG STORAGE
TEND5:	DEC	-2;		SAVE CELL FOR S AND CONTEXT FOR TAPE
TEND6:	Z
TEND7:	Z;			SAVE CELL
TEND8:	Z;			COUNT OF REWRITE TRIES


;	PAGE 34 - TAPE INTERRUPT ROUTINES

TEND9:	AOS	TERROR;   	COUNT TAPE ERRORS
TEND93:	AOS	B,TEND8;	COUNT REWRITE TRIES
	CONO	MT1,0;		OFF WITH THE LIGHTS
	TSX	TWAIT;		WAIT ON TAPE
	CONO	MTC,CDATA+3400;	BACKSPACE TAPE
	TSX	TWAIT
	CAIG	B,3;		TRY REWRITE 3 TIMES.
	JRST	TEND1

TEND95:	CONO	MTC,CDAT+5400;	WRITE BLANK TAPE
	TSX	TWAIT
	CONSZ	MT1,30;		CHECK FOR NOT ERASED
	AOS	CT45;		COUNT ERASE ERRORS
	AOS	TERASE
	SETZM	TEND8
	JRST	TEND1

TWAIT:	CONSO	MT1,40000;	WAIT FOR COMMAND OK
	JRST	.-1
	CONSO	MT1,2;		AND FOR TAPE READY
	JRST	.-1
	DONE


;	PAGE 35 - TAPE INTERRUPT ROUTINES

;	MAG TAPE OUTPUT START ROUTINE

CIO:	SKIPE	 SKR;		SKIP IF SKULKER NOT USING DC
	JRST	 CIO1
	SKIPN	 DIP;		NO START IF DISC USING DC
	CONSZ	 MT1,200
	JRST	 CIO1;		CANT WRITE ON FILE PROTECTED TAPE
	CONSO	 MT1,2;		TAPE SHOULD BE READY
	MOVE	 B,[BLKO DC,TCW]
	MOVEM	 B,40+2*CDC;	SET UP DC INTERRUPT ROUTINE
	MOVE	 B,[JSR DISMIS]
	MOVEM	 B,41+2*CDC
	HRRZ	 B,DT.BUF;	ADDRESS OF BUFFER
	HRRM	 B,TCW
	HLRZ	 B,1(B);	GET WORD COUNT
	MOVNS	 B,B
	HRLM	 B,TCW;		NEG COUNT TO CONTROL WORD
	CONO	 MT1,0;		DISABLE ALL, I DONT KNOW WHY!
	CONO	 DC,CDC+DMT+3400; START DATA OUT
	CONO	 MTC,CDAT+71000; START TAPE - BINARY @556 BPI
	CONO	 PI,2000+CHDC;	ENABLE DATA CONTROL
	SETZ	 TAPES
	CONO	 MT1,1;		ENABLE TAPE CONTROL FREE TRAP
	DONE

CIO1:	AOS	TAPES;		SET RESTART FLAG
	DONE

TAPES:	Z;			SET IF TAPE RESTART IN NEEDED
TCW:	Z;			DC CONTROL WORD FOR TAPE
TPDL:	XWD	-2,.;		PUSH LIST DURING INTERRUPT

,	TAPE RESTART ROUTINE

TRST:	SKIPN	DIP;		NO RESTART IF DISC USING DC
	SKIPN	TAPES
	DONE;			NOR IF NONE INDICATED
	CONSZ	TTY,170;    	SKIP IF NOT BUSY
	JRST	TRST1
	MOVEI	B,"";		  THATS A BELL
	DATAO	TTY,B
TRST1:	SKIPE	DT.BUF;		SKIP IF NOTHING TO START
	TSX	CIO;		START TAPE I/O
	DONE

;	PAGE 36 - CONSOLE TTY INTERRUPT ROUTINES

;	CHANNEL 6 INTERRUPTS: TTY IN, TTY OUY, AND MAG TAPE

CTYR:	Z
	CONSO	TTY,10;		IS IT AN OUTPUT INTERRUPT?
	JRST	ST41;		MUST BE INPUT
	SKIPN	CO.BUF;		SKIP IF BUFFER ATTACHED
	JRST	ST40
	EXCH	B,ST31;		SAVE REGISTER
	ILDB	B,ST32;		GET NEXT BYTE
	JUMPE	B,ST33;		DISMISS IF END CHARACTER
	DATAO	TTY,B;		OUTPUT CHARACTER
ST34:	EXCH	B,ST31;		RESTORE
	JRST	12,@CTRY;	RETURN AND DISMISS
ST33:	CONO	TTY,200+CCTY;	TURN OFF OUTPUT FLAG
	EXCH	S,ST35
	EXCH	PP,ST22
	EXCH	E,ST24
	TSX	MBA
	SKIPE	CO.BUF;		ARE THERE MORE BUFFERS TO SEND
	TSX	BIO
	EXCH	PP,ST22
	EXCH	E,ST24
	EXCH	S,ST35
	JRST	ST34

ST40:	CONO	TTY,200+CCTY
	JRST	12,@CTYR

ST22:	XWD	-2,.
ST23:	Z;			PDL FOR INTERRUPT ROUTINE
ST24:	Z
ST31:	Z
ST32:	POINT	7,0;		POINTER TO OUTPUT TEXT
ST35:	DEC	-1

;	START ROUTINE FOR CONSOLE TTY OUTPUT

BIO:	HRRZ	B,CO.BUF
	HLL	B,[POINT 7,0]
	ADDI	B,1;		POINTER TO TEXT
	MOVEM	B,ST32;		INITIALIZE
	CONO	TTY,CTTY;	ASSIGN CHANNEL
	ILDB	B,ST32
	DATAO	TTY,B;		START OUT FIRST CHARACTER
	DONE

;	PAGE 37 - CONSOLE TTY INTERRUPT ROUTINES

;	INPUT INTERRUPT FROM CONSOLE TTY

ST41:	CONSO	TTY,40;		SKIP IF TTY INPUT INTERRUPT
	JRST	TIP;		MUST BE TAPE
	EXCH	B,ST46
	EXCH	E,ST47
	EXCH	PP,ST52
	JUMPN	E,ST42;		JUMP IF WE ALREADY HAVE A BUFFER
	TSX	GETBUF
	JRST	ST45-1;		ITS TOUGH IF NO BUFFERS
	MOVEI	B,^D80;		LIMIT ON NUMBER OF INPUT CHARACTERS
	MOVEM	B,ST51
	HRRM	E,ST48;		SAVE BUFFER ADDRESS
	HLL	E,[POINT 7,0];	MAKE UP POINTER
	AOS	E;	 	TEXT IS WORD ONE
ST42:	DATAI	TTY,B;		READ THE CHARACTER
	IDPB	B,E;		DEPOSIT IN BUFFER
	SOSN	ST51;		COUNT INPUT CHARACTERS
	JRST	.+3;		NOT TOO MANY FOR THE BUFFER NOW
	CAIE	B,215;		SKIP IF CHARRIAGE RETURN
	JRST	ST45;		NOT YET - EXIT

	EXCH	D,ST49
	MOVEI	B,0
	MOVE	E,ST48;		GET BUFFER LOCATION
	AOS	E;		TO TEXT
	MOVEM	E,ST50;		SAVE IT
	MOVE	D,[POINT 7,OPMSG]

ST43:	ILDB	E,ST50
	CAIN	E,15;		SKIP IF NOT CHARRIAGE RETURN
	JEST	ST44;		CR - END OF LINE
	IDPB	E,D;		PUT CHARACTER IN MESSAGE
	AOS	B
	CAIGE	B,^D30;		SKIP IF ENOUGH CHAR FOR MESSAGE LINE
	JRST	ST43;		GO FOR ANOTHER

ST44:	MOVEM	B,L.OPM		COUNT OF CHARACTERS IN MESSAGE
	EXCH	D,ST49
	HRRZ	E,ST48;		GET BUFFER POINTER
	TSX	MBA1;		AND GIVE IT BACK
	MOVEI	E,0;		RESET BUFFER SWITCH
	CONO	TTY,CTTY+1000;	OFF WITH THE FLAG!
ST45:	EXCH	E,ST47
	EXCH	B,ST46
	EXCH	PP,ST52
	JRST	12,@CTYR

ST46:	Z;			SAVE REG B
ST47:	Z;			BYTE POINTER FOR INPUT,SAVE REG E, SIGNAL
ST48:	POINT	7,0;		CONSTANT IN LEFT, BUFFER POINTER IN RIGHT
ST49:	Z;			SAVE REG D
ST50:	Z;			HOLDS BYTE POINTER
ST51:	Z;			COUNT OF INPUT CHARACTERS
ST52:	XWD	-2,ST51-1;	PDL POINTER FOR INTERRUPT ROUTINE
