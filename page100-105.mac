SUBTTL TAPE OUTPUT ROUTINE

,	HANDLES ALL TAPE OUTPUT EXCEPT ACCOUTING FOR RECORDS

SOUT:	TSX	GETBUF
	DONE
	HRL	D,B
	HLR	D,(B);		CONTROL WORD FOR MOVE
	SUBI	E,2;		FAKE UP FOR MOVE ROUTINE
	HRLI	E,D;		CONTROL WORD ADDRESS
	TSX	BJ9.1;		MOVE WORDS TO BUFFER
	ADDI	E,2;		RESTORE
	TSW	PUTB;		START OUTPUT
	DONE

SUBTTL OUTPUT CUMULATIVE STATISTICS LINE

;	OUTPUTS TABLE OF SIZE 10 FROM (J), USES LOTS OF REGISTERS

TABO:	TSX	CMESS
	TSX	TABO1;		FORMAT THE LINE
	DONE

TABO1:	HRREI	H,-12
ST.1:	MOVEI	G,6;		FIELD SIZE
	MOVEI	I," ";		  ALTERNATE OUTPUT CHARACTER
	SETZM	D;		INITIAL OVERPUNCH
	MOVE	A,0(J);		GET ELEMENT FROM TABLE
	AOS	J
	TSX	FRMT;		FORMAT ELEMENT FOR OUTPUT LINE
	AOJN	H,ST.1
	XMT 5,ST.12
	MOVEI	A,STAT2+1
	SUB	A,F
	TSW	CTD;		OUTPUT LINE NUMBER ON STATISTICS
	DONE

ST.12:	ASCII	?   # ?

SUBTTL OUTPUT HOURLY HEADING LINE

HMES:	TSX	CMESS
	TSX	HMES1
	DONE

HMES1:	INS	12;		LINE FEED
	MOVE	A,DR
	TSX	CTD;		HOURS TO HEADING LINE
	INS	":"
	INS	" "
	XMT 10,DATE
	XMT 2,CRLF
	XMT 110,HEAD;	      72 HEADING CHARACTERS
	DONE

SUBTTL OUTPUT DETAIL STATISTICS ON THE MINUTE

ST1:	TSX	GETCT;		COMPUTE NEW COUNTS
ST1B:	SKIPE	MIN
	JRST	.+3
	SETZM	TIME;		RESET COMPUTE % ON THE HOUR
	SETZM	CT44
	MOVE	B,CT25
	ADD	B,CT26
	ADD	B,CT27A
	ADD	B,CT27B
	JUMPE	B,ST2.5-1;	NO OUTPUT IF NO ERRORS OR CHAR.
	TSX	CMESS
	TSX	ST1A;		FORMAT TO BUFFER
	HRREI	B,-CTPARS
ST2.5:	MOVE	E,CTPAR+CTPARS(B);  GET PARAMETER WORD
	TLNN	E,400000;	SKIP IF SHOULD NOT ZERO THE COUNTER
	SETZM	(E)
	AOJL	B,ST2.5
ST2.6:	DONE

;	FORMAT A LINE FOR THE CONSOLE TTY

ST1A:	MOVE	A,MIN
	TSX	CTD;		CONVERT MINUTES
	HRREI	H,-CTPARS;	TABLE SIZE
ST2:	MOVEI	I," ";		SET ALTERNATE OUTPUT CHARACTER
	SETZM	D;		INITIALIZE TO NO OVERPUNCH
	LDB	G,[POINT 5,CTPAR+CTPARS(H),5]; GET FIELD SIZE
	LDB	F,[POINT 12,CTPAR+CTPARS(H),17]; GET SCALE FACTOR
	MOVE	B,CTPAR+CTPARS(H)
	MOVE	A,(B);		GET COUNT
	JUMPN	A,.+3
	MOVEI	D,".";		  ALTERNATE CHAR IF ALL ZERO
	JRST	ST2.4
	IDIV	A,F;		SCALE
	JUMPN	A,ST2.R;	GET REMAINDER IF QUOTIENT IT ZERO
	MOVE	A,B
	IMULI	A,^D10;		MAKE A PLACE FOR THE *
	MOVEI	D,"*";		  MAY HELP BUT IT AINT PERFECT
ST2.4:	TSX	FRMT;		FORMAT THE OUTPUT LINE
	SOJL	H,ST2;		GO AROUND IF MORE COUNTERS
	DONE

SUBTTL ROUTINE TO UPDATE THE DATE

UPDATE:	AOS	B,DAY;		ROUTINE TO INCREMENT AND UPDATE THE DATE
	MOVE	D,[POINT 5,MONS]
	MOVE	C,MONTH
	IBP	D
	SOJN	C,.-1;		COUNT DOWN TO THE CURRENT MONTH
	MOVE	C,MONTH
	LDB	A,D
	CAIE	C,2
	JRST	UD2;		NOT FEBRUARY
	MOVE	C,YEAR
	TRNN	C,3;		CHECK FOR LEAP YEAR
	AOSA	A;		LEAP YEAR - AN EXTRA FOR FEB
UD2:	SUB	B,A
	JUMPLE	B,UP1;		NOT A NEW MONTH
	AOS	B,MONTH;	BUMP THE MONTH
	MOVEI	A,1
	MOVEM	A,DAY;		SET DAY = 1
	TSX	MDS;		TIME FOR DISC ACCOUNTING
	MOVE	B,MONTH
	SUBI	B,^D12
	JUMPLE	B,UP1;		NOT A NEW YEAR
	AOS	YEAR
	MOVEI	A,1
	MOVEM	A,MONTH;	SET MONTH = 1

,	CONVERT DATE AND TIME TO ASCII

UP1:	MOVE	A,BLANKS
	MOVEM	A,DATE
	MOVEM	A,DATE +1;	BLANK THE DATE
	MOVEM	A,ATIM;		AND TIME
	MOVE	C,[POINT 7,DATE]
	MOVE	A,MONTH
	TSX	CTDB
	INS	"/"
	MOVE	A,DAY
	TSX	CTDB
	INS	"/"
	MOVE	A,YEAR
	TSX	CTDB
	MOVE	C,[POINT 7,ATIM]
	MOVE	A,HR
	TSX	CTDB;		CONVERT - SUPPRESS LEAD ZEROS
	INS	":"
	MOVE	A,MIN
	TSX	CTD;		CONVERT - WITH LEAD ZEROS
	DONE

ADATE=UP1
	RADIX	10
MONS:	BYTE	(5)31,28,31,30,31,30,31,31,30,31,30,31

	RADIX	8

SUBTTL FORMAT A COUNTER FOR CONSOLE OUTPUT

;	A CONTAINS SCALED COUNTER VALUE, C THE POINTER TO THE
;	OUTPUT LINE, G THE FIELD SIZE, AND I THE ALTERNATE
;	OUTPUT CHARACTER.
;	D, B ARE CLOBBERED.

FRMT:	CAMGE	A,T80(G);	TOO BIT FOR FIELD?
	JRST	ST4;		NO
	MOVEI	D,"!";		  YES, SET OVERPUNCH
ST3:	IDIVI	A,^D10;		SCALE BY TEN
	CAML	A,T80(G);	IF STILL TOO BIG:
	AOJA	D,ST3;		BUMP TO NEXT SPECIAL CHARACTER
ST4:	CAIN	G,1
	MOVEI	I,"0";		  SET ALTERNATE TO ZERO ON LAST COLUMN
	IDIV	A,T80.99(G)
	JUMPN	A,ST5
	MOVE	A,I;		IF ZERO GET ALTERNATE
	JRST	ST6
ST5:	MOVEI	I,"0";		  SET ALTERNATE TO ZERO SINCE WE NOW HAVE A DIG
	ORI	A,60;		ASCII BITS
ST6:	IDPB	A,C;		DUMP IN OUTPUT
	EXCH	A,A+1;		GET REMAINDER
	SOJG	G,ST4;		GO AROUND IF MORE COLUMNS
	DPB	D,C;		DEPOSIT OVERPUNCH
ST7:	DONE
