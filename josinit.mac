; PG 1

;	JOSS INITIALIZATION AND RECOVERY PROGRAMS

;	     G.E. BRYAN

;	ASSEMBLED 8/1/67 FROM TAPE 56 (A SEQUENCE COPY OF 31)
;	   BINARIES ON TAPE 24 AS IU

; PG 2

;	INITIALIZATION DIALOG FOR JOSS STARTUP.

;	THIS CODE MUST BE LOADED LAST, DIRECTLY
;	PRECEDED BY DDT; A (THE ARITHMETIC) MUST
;	BE LOADED FIRST TO PRESERVE A DDT ENTRY AT 140.

;	ASSEMBLY PARAMETERS FOR THE PI CHANNELS

CDC=1;  1 - DATA CONTROL
CDRM=2; 2 - I/O PROCESSOR AND DRUM CONTROL
CI630=3;	3 - 630 INPUT
CO630=4;	4 - 630 OUTPUT
CDAT=6;		INTERRUPT CHANNEL FOR TAPE
CCTY=6; 6 - CONSOLE TELETYPE
CAPR=7; 7 - PROCESSOR

CHDC=100;	THESE ARE CORRESPONDING DEFINITIONS FOR PI CONOS
CHDRM=40;
CH630=30
CHDAT=0
CHCTY=2
CHAPR=1
MTC=220;			DEVICE CODES FOR MAG TAPE REGISTERS
MT1=224
MT2=230
DR=400
DP=010
DMT=20;		DATA CONTROL DEVICE NUMBER FOR MAG TAPE 
DDK=50;		AND FOR DISC

A=0
B=1
C=2
D=3
E=4
F=5
G=6
H=7
I=10
J=11
K=12;		USED BY DISTRIBUTOR
L=13;		USED BY DISTRIBUTOR
M=14;		USED BY DISTRIBUTOR
N=15;		USED BY DISTRIBUTOR
S=16;		USED FOR STATION INDEX
PP=17;		PUSH-POP REGISTER

	DEFINE XMT  (M,A); TRANSMIT M CHARACTERS FROM A
<OPDEF X[M'B12]
X A>

	OPDEF	TSX[PUSHJ PP,0]
	OPDEF	DONE[POPJ PP, 0]

; PG 3

	EXTERN DDT,MONTH,YEAR,HR,MIN,ADATE,DATE
	EXTERN  CMESS,PROP,DRMR,C25,C26,CTYR,APRR
	EXTERN  OCTW
	EXTERN  CORE,C20,PPW,TYPE6,SOUT,HMES,SIGPR

	INTERN  DUMP,RJD,WJD,DATIME
	INTERN  SWITCH,FAKE,L100,BEGIN



DISMIS:	Z
	CONO	PI,100+CHDC;	TURN OFF INTERRUPT CHANNEL
	JRST	12,@DISMIS;	DISMISS INTERRUPT

; PG 4

SAVE=120;	SAVE LOCATION FOR THE ACS
IOA=777700
DUMP:	CONO	PI,200;		TURN ON THE INTERRUPT SYSTEM
	EXCH	B,[XWD 0,SAVE]
	BLT	B,SAVE+17;	SAVE THE ACCUMULATORS
	EXCH	B,[XWD 0,SAVE]
	MOVEM	B,SAVE+1
	CONO	MTC,0
	CONO	MT1,2;		READY UNIT ZERO
	MOVE	B,[JSR DISMIS]
	MOVEM	B,41+2*CDC
	MOVE	B,[BLKO DC,S]
	MOVEM	B,40+2*CDC
	MOVEI	E,0;		LOCATION COUNTER
	MOVEI	F,20;		A BLANK
	MOVE	D,[POINT 3,SAVE]
	JRST	TD1;		FIRST THE ACCUMULATORS
TD05:	CAIN	E,20;		AND THEN CORE
	MOVE	D,[POINT 3,20];	INPUT POINTER
TD1:	MOVE	G,[POINT 6,IOA];  OUTPUT POINTER
	MOVEI	B,0
	IDPB	F,G;		CARRIAGE CONTROL
	MOVE	C,[POINT 3,E,^D20]; POINT TO LOCATION COUNTER
TD2:	ILDB	A,C
	IDPB	A,G;		ADDRESS
	CAIGE	B,4
	AOJA	B,TD2
	MOVEI	B,10;		EIGHT WORDS PER LINE
TD3:	IDPB	F,G
	IDPB	F,G;		TWO BLANKS BETWEEN WORDS
TD4:	MOVEI	H,14;		TWELVE CHARACTERS PER WORD
	ILDB	A,D
	IDPB	A,G;		CONVERT TO BCD
	SOJG	H,.-2
	SOJG	B,TD3;		JUMP IF LINE NOT FULL
	IDPB	F,G
	IDPB	F,G
	CONSO	MT1,40000;	WAIT FOR XFER NEW COMMAND
	JRST	.-1;		WAIT FOR TAPE CONTROL FREE
	MOVE	S,[XWD -^D20,IOA-1];  DATA CONTROL WORD
	CONO	DC,CDC+DMT+3400
	CONO	MTC,31000
	CONO	PI,2000+CHDC
	CONSZ	PI,CHDC;	WAIT FOR DATA CONTROL FINISHED
	JRST	,-1;		WAIT FOR  END OF RECORD
	ADDI	E,^D8
	CAIGE	E,100000;	AINT NONE UP THERE
	JRST	TD05
	CONSO	MT1,40000
	JRST	.-1
	CONO	MTC,1400;	END FILE
	CONSO	MT1,40000;	WAIT FOR COMMAND OK
	JRST	.-1
	CONO	MTC,400;	REWIND
	JRST	DDT;		GO TO DDT

; PG 5

DATIME:	CONO	TTY,3600;	ALL TTY FLAGS OFF
	TSX	SLM;		SET LOW MEMORY
	TSX	ADATE;		CONVERT TO ASCII
	TSX	D50;		ASK ABOUT DDT
	TSX	DTO;		OUTPUT CURRENT DATE AND TIME

D1:	SETZB	D,H;		SLASH COUNT AND CONVERTER
	TSX	LM
	MOVEI	B,6
	MOVE	C,[POINT 7,S1]
	JSP	G, TS;		SEND "DATE: "
D2:	JSP	G,TTCH;		GET TTY CHARACTER
	CAIN	F,"/"
	JRST	D10;		ITS A SLASH
	CAIN	F,15
	JRST	D12;		ITS A CR
	ANDI	F,160
	CAIE	F,60
	JRST	D1;		NON-NUMERIC -- TRY AGAIN
	DATAI	TTY,F;		RECOVER CHARACTER
	ANDI	F,17;		MASK OFF ASCII BITS
	IMULI	D,^D10
	ADD	D,F
	CAIL	D,^D100;
	JRST	D1;		DATE COMPONENT MUST BE < 100
	JRST	D2;		GET ANOTHER

D10:	CAIL	H,2;		HERE WE HAVE A SLASH
	JRST	D1;		BUT ONLY 2 ARE ALLOWED
	JUMPE	D,D1;		AND I MUST BE A NUMBER > 0
	CAMLE	D,R1(H);	CHECK RANGE
	JRST	D1;		GO ASK AGAIN
	MOVEM	D,MONTH(H);	SAVE MONTH OR DAY
	SETZM	D;		ZERO CONVERTER
	AOJA	H,D2

D12:	JUMPE	D,D14;		HERE A CR, IF NOT DIGITS USE OLD VALUES
	CAIGE	D,^D66;		CHECK YEAR
	JRST	D1;		NOT UP TO DATE
	MOVEM	D,YEAR

; PG 6

D14:	SETZM	D
	TSX	LM
	MOVEI	B,6
	MOVE	C,[POINT 7,S2]
	JSP	G,TS
D16:	JSP	G,TTCH;		GET A CHAR FROM  TTY
	CAIN	F,":"
	JRST	D20;		ITS A COLON
	CAIN	F,15
	JRST	D22;		ITS A CR
	ANDI	F,160
	CAIE	F,60;		MUST BE NUMERIC
	JRST	D14;		NON-NUMERIC
	DATAI	TTY,F;		RECOVER CHARACTER
	ANDI	F,17;		MASK TO NUMERIC
	IMULI	D,^D10
	ADD	D,F;		CONVERT TO BINARY
	CAIL	D,^D60;
	JRST	D14;		TIME COMPONENT MUST BE < 60
	JRST	D16;		GET NEXT

D20:	MOVEM	D,HR
	SETZM	D
	JRST	D16

D22:	MOVEM	D,MIN
	TSX	ADATE;		CONVERT TO ASCII
	TSX	WJD;		DUMP SYSTEM ON DRUM
	DONE;			DONE WITH INITIALIZATION

R1:	DEC	12;		BIGGEST MONTH
	DEC	31;		BIGGEST DAY

; PG 7

;	ASK ABOUT DDT AND ALLOCATE USE OF USER BLOCKS ACCORDINGLY

D50:	MOVEI	J,DATIME
	CAIGE	J,40000
	JRST	D70;		DDT AVAILABLE ANYWAY
	TSX	LM
	MOVEI	B,^D10
	MOVE	C,[POINT 7,S3]
	JSP	G,TS;		ASK FOR DDT

D51:	JSP	G,TTCH;		GET TTY CHAR
	CAIN	F,"Y"
	JRST	D70;		YES - DDT
	CAIN	F,"N"
	JRST	D60;		NO DDT
	JRST	D50;		BAD CHAR - TRY AGAIN

D60:	TSX	LM
	MOVEI	B,6
	MOVE	C,[POINT 7,S5]
	JSP	G,TS;		SAY: NO DDT
	TSX	LM
	MOVEI	J,DDT
	JRST	D71

D70:	TSX	LM
	MOVEI	B,^D13
	MOVE	C,[POINT 7,S4]
	JSP	G,TS;		SAY: DDT AVAILABLE
	TSX	LM
D71:	MOVEI	C,0
D73:	CAIGE	J,40000
	DONE
	SETOM	CORE(C);	BUSY BLOCKS USED BY SYSTEM
	SUBI	J,2000
	AOJA	C,D73

; PG 8

;	WRITE AND READ CONSOLE TTY

,	SEND STRING TO CONSOLE TTY
,		B IS CHAR COUNT, C IS POINTER, F IS USED

TS:	ILDB	F,C
	DATAO	TTY,F
	CONSO	TTY,10;
	JRST	.-1;
	CONO	TTY,200;
	SOSE	B;
	JRST	TS;
	JRST	(G);

S1:	ASCII	$DATE: $

S2:	ASCII	$TIME: $

S3:	ASCII	$DDT?-Y/N: $

S4:	ASCII	$DDT AVAILABLE$

S5:	ASCII	$NO DDT$

,	OUTPUT DATE AND TIME TO CONSOLE

DTO:	TSX	LM
	MOVEI	B,^D15
	MOVE	C,PDATE
	JSP	G,TS
	TSX	LM
	DONE

,	READ AND ECHO ON TTY

TTCH:	CONSO	TTY,40
	JRST	.-1;		WAIT FOR A CHAR
	CONO	TTY,1000;	FLAG OFF
	DATAI	TTY,F;		READ CHAR
	DATAO	TTY,F;		ECHO BACK
	CONSO	TTY,10
	JRST	.-1;		WAIT ON OUTPUT
	CONO	TTY,200;	FLAG OFF
	ANDI	F,177;		JUST 7 BITS
	JRST	(G);		RETURN

,	MOVE CARRIER TO LEFT MARGIN

LM:	MOVE	B,2
	MOVE	C,[POINT 7,CRLF]
	JSP	G,TS
	DONE

CRLF:	OCT	064240000000;	CARIAGE RETURN,LINE FEED AND ZERO
PDATE:	POINT	7,DATE;		POINT TO DATE

; PG 9

;	ENTERED FOLLOWING READING JOSS FROM DRUM

RECOVER:MOVE	C,42
	MOVE	D,43
	MOVEI	B,ACS;		PLACE TO SAVE THE ACS FOR A MOMENT
	BLT	B,ACS+17
	MOVE	B,30
	MOVEM	B,ACS+1;	AND REG 1
	XCT	BEG3;		SET UP PUSH REGISTER
	TSX	CLEAR;		CLEAR MACHINE FLAGS
	TSX	DISPLAY;	SEND ERROR MESSAGE AND DUMP
	TSX	RESTORE;	SET UP TO GO
	CONO	TTY,6;		ENABLE TTY OUTPUT CHANNEL
	CONO	PI,2402;	ENABLE CHANNEL FOR TTY
	CONSZ	TTY,20
	JRST	.-1;		WAIT UNTIL NON-BUSY
	TSX	ADATE
	JRST	BEG5;		AND GO.

; PG 10

CLEAR:	MOVEI	B,7
	JRST	10,.+1;		DISMISS ALL INTERRUPTS
	SOJG	B,.-1
	CONO	TTY,1200;	TTY FLAGS OFF
	CONO	APR,111000;	PROCESSOR FLAGS OFF
	CONO	MT1,0;		TAPE INTERRUPTS OFF
	CONO	DC,0;		CLEAR DATA CONTROL
	CONO	270,0;		CLEAR DISC FILE
	DONE

; PG 11

DISPLAY:TSX	CMESS
	TSX	MR0;		SEND ERROR NUMBER
	TSX	CMESS
	XMT ^D13,M100;        AC TITLE
	MOVEI	F,ACS;		PLACE THE ACS ARE STORED
EC1:	TSX	CMESS;		DISPLAY ACS
	TSX	MR1
	CAIGE	F,ACS+20
	JRST	EC1
	TSX	CMESS
	XMT ^D50,M101;	      VARIOUS @ CELLS
	MOVE	B,ACS+11;	REGISTER 11
	MOVEM	B,SA
	MOVEI	F,SA
	TSX	CMESS
	TSX	MR1
	TSX	CMESS
	XMT 5,M102; MORE LINE FEEDS
	DONE

; PG 12

;	MAKE UP HALT NUMBER MESSAGE
MR0:	MOVE	F,40;
	XMT ^D18,M102

	MOVE	A,F
	TSX	OCTW
	DONE

;	FORMAT FOUR OCTAL WORDS FROM (F) INTO A LINE

MR1:	MOVEI	G,4
MR1.1:	MOVE	A,(F)
	TSX	O1
	AOS	F
	SOJG	G,MR1.1
	DONE

O1:	TSX	OCTW
	XMT 3,BLANKS
	DONE

M100:	ASCII	?ACCUMULATORS:?

M101:	ASCII	?OFFENDING LOCATION (11):  CONTENTS(@11): @17: @0:?









M102:	OCT	064241205024;	CR AND 4 LFS
	ASCII	?HALT NUMBER: ?



; PG 13

RESTORE:MOVE	B,RE1
	BLT	B,MIN
	TSX	SLM;		SET LOWER MEMORY
	DONE

RE1:	XWD	31,YEAR
BLANKS:	ASCII	?     ?

; SET UP LOWER MEMORY

SLM:	MOVE	A,[XWD LOC41,41]
	BLT	A,137
	DONE

; PG 14

;	0-17	EXEC LOADER (SHADOW MEMORY)
;	20-27	PAPER TAPE LOADER - RIM
;	30	SAVE CELL FOR REG 1 DURING RECOVERY
;	31-35	SAVE CELLS FOR DATE AND TIME IN RECOVERY
;	36-37	USED BY DDT
;	40-41	UUO CELLS
;	42-57	INTERRUPT SYSTEM TRAP CELLS
;	60-77	PART OF DRUM READ ROUTINE
;	100	PSEUDO SWITCHES
;	101	DUMP ENTRY
;	102	PSEUDO CONSOLE SIGNAL CELL
;	103	ENTRY TO READ DRUM ROUTINE
;	104	DRUM READ AND WRITE ROUTINES

; PG 15

;	CONTENTS OF CELLS 41-137
LOC41:	JSR	PROP
	Z
	JSR	DDT;		43
	JSR	DRMR;		44 - CHANNEL 2, THE DRUM
	Z
	JSR	C25;		46 - CHANNEL 3, 630 INPUT
	Z
	JSR	C26;		48 - CHANNEL 4, 630 OUTPUT
	Z
	JRST	DDT;		52 - CHANNEL 5, DISC FILE
	Z
	JSR	CTYR;		54 - CHANNEL 6, TAPE AND CONSOLE TTY
	Z
	JSR	APRR;		56 - CHANNEL 7, PROCESSOR
	Z

;	DEFINITIONS FOR WHEN DRUM CODE IS MOVED TO LOW MEMORY

BTW=116
RJ=60
RIO=72
DS=73
DS1=74
SWITCH=100
FAKE=102
RJD=103
WJD=117
WD=132
RC=135
SA=112
CKD=63

; PG 16

;	READ AND WRITE JOSS ON DRUM

RJ.:	CONO	DP,0;		167 TO READ
	JSP	D,DS1
	CONO	DR,230;		READ
CKD.:	CONSO	DR,1000;	CHECK DRUM ERRORS
	CONSZ	DP,100060;	IOP ERRORS
	JRST	4,7770
	CONSO	DR,100;		WAIT FOR DONE
	JRST	CKD
	CONO	DR,270;		DESELECT
	JRST	(C)
RIO.:	XWD	-40000,40000;	I/O WORD - 16K FROM UPPER BOX
DS.:	CONO	DP,100;		SELECT FOR WRITE
DS1.:	DATAO	DP,RIO;		DRUM SELECT
	DATAO	DP,B;		UNIT AND TRACK
	CONO	DR,260;		SELECT
	JRST	(D)

HALTS=20000
L100:	Z;	100 - PSEUDO SWITCH CELL
	JRST	DUMP
FAKE.:	Z;			102 - PSEUDO SIGNAL CELL
RJD.:	MOVEI	B,0;		103 - RECOVERY ENTRANCE
	JSP	C,RJ
	MOVE	B, BTW;		MOVE TO LOW MEMORY
	BLT	B,37777
	MOVEI	B,2000
	JSP	C,RJ;		FILL UPPER BOX
	JRST	RECOVER
SA.:	BLOCK	4;		FOUR SAVE CELLS
BTW.:	XWD	40140,140;	BLT CONTROL WORD
WJD.:	MOVEI	B,2000;		WRITE JOSS ON DRUM
	JSP	C,WD;		WRITE
	JSP	C,RC;		READ COMPARE
	MOVEI	B,40000
	BLT	B,77777;	MOVE LOW TO HIGH MEMORY
	MOVEI	B,0
	JSP	C,WD;		WRITE LOW BOX
	JSP	C,RC;		READ COMPARE
	MOVEI	B,2000
	JSP	C,RJ;		READ BACK UPPER BOX
	DONE

WD.:	JSP	D,DS;		WRITE DRUM - SELECT
	CONO	DR,220;		WRITE DRUM
	JRST	CKD;		GO CHECK

RC.:	JSP	D,DS;		READ COMPARE - SELECT
	CONO	DR,210;		COMPARE DRUM AND CORE
	JRST	CKD
ACS:	BLOCK	20;		TEMP STORAGE FOR ACS

; PG 17

BEGIN:	CONO	APR,210000;	RESET STUFF
BEG3:	MOVE	PP,PPW
	TSX	DATIME;		ASK FOR DATE AND TIME
	CONO	TTY,206;	TURN OFF FTO FLAG AND ENABLE CHANNEL
BEG5:	TSX	C20;		INITIALIZE IRWIN
	CONO	APR,122007;	SET PROCESSOR FLAGS AND CHANNEL
	CONO	MTC,0
	CONO	MT1,2;		GET TAPE DRIVE ZERO READY
	CONO	PI,20000;	ENABLE PARITY ERROR TRAP
	HRREI	S,-2;		CONTEXT FOR TAPE
	MOVEI	B,TYPE6
	TSX	SOUT
	HRREI	S,-1;		CONTEXT FOR CONSOLE TTY
	TSX	HMES;		LOAD HEADING MESSAGE
	JRST	SIGPR

	END
