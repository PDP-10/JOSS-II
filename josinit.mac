; PG 1

;	JOSS INITIALIZATION AND RECOVERY PROGRAMS

;	     G.E. BRYAN

;	ASSEMBLED 8/1/67 FROM TAPE 56 (A SEQUENCE COPY OF 31)
;	   BINARIES ON TAPE 24 AS IU

; PG 2

;	INITIALIZATION DIALOG FOR JOSS STARTUP.

;	THIS CODE MUST BE LOADED LAST, DIRECTLY
;	PRECEDED BY DDT; A (THE ARITHMETIC) MUST
;	BE LOADED FIRST TO PRESERVE A DDT ENTRY AT 140.

;	ASSEMBLY PARAMETERS FOR THE PI CHANNELS

CDC=1;  1 - DATA CONTROL
CDRM=2; 2 - I/O PROCESSOR AND DRUM CONTROL
CI630=3;	3 - 630 INPUT
CO630=4;	4 - 630 OUTPUT
CDAT=6;		INTERRUPT CHANNEL FOR TAPE
CCTY=6; 6 - CONSOLE TELETYPE
CAPR=7; 7 - PROCESSOR

CHDC=100;	THESE ARE CORRESPONDING DEFINITIONS FOR PI CONOS
CHDRM=40;
CH630=30
CHDAT=0
CHCTY=2
CHAPR=1
MTC=220;			DEVICE CODES FOR MAG TAPE REGISTERS
MT1=224
MT2=230
DR=400
DP=010
DMT=20;		DATA CONTROL DEVICE NUMBER FOR MAG TAPE 
DDK=50;		AND FOR DISC

A=0
B=1
C=2
D=3
E=4
F=5
G=6
H=7
I=10
J=11
K=12;		USED BY DISTRIBUTOR
L=13;		USED BY DISTRIBUTOR
M=14;		USED BY DISTRIBUTOR
N=15;		USED BY DISTRIBUTOR
S=16;		USED FOR STATION INDEX
PP=17;		PUSH-POP REGISTER

	DEFINE XMT  (M,A); TRANSMIT M CHARACTERS FROM A
<OPDEF X[M'B12]
X A>

	OPDEF	TSX[PUSHJ PP,0]
	OPDEF	DONE[POPJ PP, 0]

; PG 3

	EXTERN DDT,MONTH,YEAR,HR,MIN,ADATE,DATE
	EXTERN  CMESS,PROP,DRMR,C25,C26,CTYR,APRR
	EXTERN  OCTW
	EXTERN  CORE,C20,PPW,TYPE6,SOUT,HMES,SIGPR

	INTERN  DUMP,RJD,WJD,DATIME
	INTERN  SWITCH,FAKE,L100,BEGIN



DISMIS:	Z
	CONO	PI,100+CHDC;	TURN OFF INTERRUPT CHANNEL
	JRST	12,@DISMIS;	DISMISS INTERRUPT

; PG 4

SAVE=120;	SAVE LOCATION FOR THE ACS
IOA=777700
DUMP:	CONO	PI,200;		TURN ON THE INTERRUPT SYSTEM
	EXCH	B,[XWD 0,SAVE]
	BLT	B,SAVE+17;	SAVE THE ACCUMULATORS
	EXCH	B,[XWD 0,SAVE]
	MODEM	B,SAVE+1
	CONO	MTC,0
	CONO	MT1,2;		READY UNIT ZERO
	MOVE	B,[JSR DISMIS]
	MOVEM	B,41+2*CDC
	MOVE	B,[BLKO DC,S]
	MOVEM	B,40+2*CDC
	MOVEI	E,0;		LOCATION COUNTER
	MOVEI	F,20;		A BLANK
	MOVE	D,[POINT 3,SAVE]
	JRST	TD1;		FIRST THE ACCUMULATORS
TD05:	CAIN	E,20;		AND THEN CORE
	MOVE	D,[POINT 3,20];	INPUT POINTER
TD1:	MOVE	G,[POINT 6,IOA];  OUTPUT POINTER
	MOVEI	B,0
	IDPB	F,G;		CARRIAGE CONTROL
	MOVE	C,[POINT 3,E,+D20]; POINT TO LOCATION COUNTER
TD2:	ILDB	A,C
	IDPB	A,G;		ADDRESS
	CAIGE	B,4
	AOJA	B,TD2
	MOVEI	B,10;		EIGHT WORDS PER LINE
TD3:	IDPB	F,G
	IDPB	F,G;		TWO BLANKS BETWEEN WORDS
TD4:	MOVEI	H,14;		TWELVE CHARACTERS PER WORD
	ILDB	A,D
	IDPB	A,G;		CONVERT TO BCD
	SOJG	H,.-2
	SOJG	B,TD3;		JUMP IF LINE NOT FULL
	IDPB	F,G
	IDPB	F,G
	CONSO	MT1,40000;	WAIT FOR XFER NEW COMMAND
	JRST	.-1;		WAIT FOR TAPE CONTROL FREE
	MOVE	S,[XWD -+D20,IOA-1];  DATA CONTROL WORD
	CONO	DC,CDC+DMT+3400
	CONO	MTC,31000
	CONO	PI,2000+CHDC
	CONSZ	PI,CHDC;	WAIT FOR DATA CONTROL FINISHED
	JRST	,-1;		WAIT FOR  END OF RECORD
	ADDI	E,+D8
	CAIGE	E,100000;	AINT NONE UP THERE
	JRST	TD05
	CONSO	MT1,40000
	JRST	.-1
	CONO	MTC,1400;	END FILE
	CONSO	MT1,40000;	WAIT FOR COMMAND OK
	JRST	.-1
	CONO	MTC,400;	REWIND
	JRST	DDT;		GO TO DDT
